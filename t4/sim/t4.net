.options savecurrents

* PHILIPS BJT'S
.MODEL BC557A PNP(IS=2.059E-14 ISE=2.971f ISC=1.339E-14 XTI=3 BF=227.3 BR=7.69 IKF=0.08719 IKR=0.07646 XTB=1.5 VAF=37.2 VAR=11.42 VJE=0.5912 VJC=0.1 RE=0.688 RC=0.6437 RB=1 RBM=1 IRB=1E-06 CJE=1.4E-11 CJC=1.113E-11 XCJC=0.6288 FC=0.7947 NF=1.003 NR=1.007 NE=1.316 NC=1.15 MJE=0.3572 MJC=0.3414 TF=7.046E-10 TR=1m2 ITF=0.1947 VTF=5.367 XTF=4.217 EG=1.11)
.MODEL BC547A NPN(IS=1.533E-14 ISE=7.932E-16 ISC=8.305E-14 XTI=3 BF=178.7 BR=8.628 IKF=0.1216 IKR=0.1121 XTB=1.5 VAF=69.7 VAR=44.7 VJE=0.4209 VJC=0.2 RE=0.6395 RC=0.6508 RB=1 RBM=1 IRB=1E-06 CJE=1.61E-11 CJC=4.388p XCJC=0.6193 FC=0.7762 NF=1.002 NR=1.004 NE=1.436 NC=1.207 MJE=0.3071 MJC=0.2793 TF=4.995E-10 TR=1m2 ITF=0.7021 VTF=3.523 XTF=139 EG=1.11)

Vcc vcc 0 12.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)
Rin in in2 100

* input coupling capacitor
Ci in2 base 0.1m

* bias circuit
R1 vcc base 80k
R2 base 0 20k

* gain stage
Q1 coll base emit BC547A
Rc vcc coll 0.9k
Re emit 0 200

* bypass capacitor
Cb emit 0 2.5m


* output stage
Q2 0 coll emit2 BC557A
Rout emit2 vcc 50

* output coupling capacitor
Co emit2 out 1.5m

* load
RL out 0 8

.op
.end

.control

set hcopypscolor=0
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0

print all

* time analysis
tran 1e-5 1e-2

echo "OP_TAB"
print vcc[0]
print in[0]
print in2[0]
print base[0]
print coll[0]
print emit[0]
print emit2[0]
print out[0]
echo "OP_END"

plot v(coll) v(out) v(in2) 
hardcopy comp.ps v(coll) v(out) v(in2)
echo comp_FIG


* frequency analysis
ac dec 10 10 100MEG
plot vdb(coll)-vdb(in2)
*plot vp(coll)
hardcopy stage.ps vdb(coll)-vdb(in2)
echo stage_FIG

plot vdb(out)-vdb(in2)
*plot vp(out)
hardcopy output.ps vdb(out)-vdb(in2)
echo output_FIG

let transfer = vdb(out)-vdb(in2)
let transferv = vm(out)/vm(in2)

meas ac topo MAX transfer
meas ac topo2 MAX transferv
let gainv = topo2
let gain = topo
let zone = topo - 3
meas ac lower WHEN transfer = zone
meas ac upper WHEN transfer = zone CROSS = LAST
let bandwidth = upper - lower

let cost = 0.2 + 0.1 + 0.008 + (80 + 20 + 0.9 + 0.2 + 0.05) + (2500 + 100 + 1500)
let merit = (gainv*bandwidth)/(cost*lower)

print gain gainv lower upper bandwidth cost merit

*input impedance in kohm
*plot -v(in2)/Vin#branch*0.001
print -v(in2)[25]/Vin#branch[25]*0.001
let Zi = abs(-v(in2)[25]/Vin#branch[25]*0.001)
print Zi

echo "data_TAB"
echo "Gain  = $&gainv"
echo "Gain(dB) = $&gain"
echo "Lower cut-off frequency = $&lower Hz"
echo "Upper cut-off frequency = $&upper Hz"
echo "Bandwidth = $&bandwidth Hz"
echo "Input impedance = $&Zi kOhm"
echo "data_END"

echo "merit_TAB"
print cost merit
echo "merit_END"

*need a different setup to measure output impedance

quit

.endc
